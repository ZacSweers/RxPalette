subprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    group = GROUP
    version = VERSION_NAME

    // The default 'assemble' task only applies to normal variants. Add test variants as well.
    afterEvaluate {
        android.testVariants.all { variant ->
            tasks.getByName('assemble').dependsOn variant.assemble
        }

        // TODO Why does android studio hiccup on this?
//        tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask.getClass()) { task ->
//            task.doFirst {
//                logging.level = LogLevel.INFO
//            }
//            task.doLast {
//                logging.level = LogLevel.LIFECYCLE
//            }
//        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    androidPlugin = 'com.android.tools.build:gradle:2.0.0-alpha9'
    minSdkVersion = 14
    targetSdkVersion = 23
    compileSdkVersion = 23
    buildToolsVersion = '23.0.2'

    ci = 'true'.equals(System.getenv('CI'))

    def kotlinVersion = '1.0.0-beta-4589'
    kotlinPlugin = "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    kotlinStdlib = "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def supportVersion = '23.1.1'
    supportAnnotations = "com.android.support:support-annotations:$supportVersion"
    supportV4 = "com.android.support:support-v4:$supportVersion"
    supportPalette = "com.android.support:palette-v7:$supportVersion"

    supportTestRunner = 'com.android.support.test:runner:0.4'
    supportTestRules = 'com.android.support.test:rules:0.4'
    supportTestEspresso = 'com.android.support.test.espresso:espresso-core:2.2.1'
    supportTestEspressoContrib = 'com.android.support.test.espresso:espresso-contrib:2.2.1'
    rxJava = 'io.reactivex:rxjava:1.1.0'
    junit = 'junit:junit:4.12'
    robolectric = 'org.robolectric:robolectric:3.0'
    truth = 'com.google.truth:truth:0.27'

    // Sample app dependencies
    supportAppCompat = "com.android.support:appcompat-v7:$supportVersion"
    rxAndroid = 'io.reactivex:rxandroid:1.1.0'
    supportRecyclerView = "com.android.support:recyclerview-v7:$supportVersion"
    butterKnife = 'com.jakewharton:butterknife:7.0.1'
    glide = 'com.github.bumptech.glide:glide:3.7.0'
    glideOkhttp = 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    okhttp3 = 'com.squareup.okhttp3:okhttp:3.0.1'

    def retrofitVersion = '2.0.0-beta3'
    retrofit = "com.squareup.retrofit2:retrofit:$retrofitVersion"
    retrofitMoshi = "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    retrofitRxJava = "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
